parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
    import fr.ensimag.deca.tools.SymbolTable;
    import fr.ensimag.deca.tools.SymbolTable.Symbol;
    import fr.ensimag.deca.DecacCompiler;
    import fr.ensimag.deca.tree.Visibility;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        } (COMMA dv2=decl_var[$t] {
            $l.add($dv2.tree);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {//par défaut
            AbstractInitialization init = null;
             
            
        }
    : i=ident {
        }
      (EQUALS e=expr {//initialization prends place
            assert($e.tree != null);
            init = new Initialization($e.tree);
            setLocation(init,$EQUALS);
        }
      )? {
            if (init == null){ init = new NoInitialization();  }
            assert($i.tree != null);   
            $tree = new DeclVar($t, $i.tree, init);
            setLocation($tree, $i.start);
            
        }
    ;

list_inst returns[ListInst tree]
@init {
    $tree = new ListInst();
}
    : (inst {
        $tree.add($inst.tree);
        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $e1.tree;
          
        }
    | SEMI {// rien se passe
            Location loc = new Location($SEMI.line, $SEMI.getCharPositionInLine()+1, getInputStream().getSourceName());
            $tree = new NoOperation(loc);
        }
    | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false, $list_expr.tree);
            setLocation($tree, $PRINT);
        }
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(false, $list_expr.tree);
            setLocation($tree, $PRINTLN);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(true, $list_expr.tree);
            setLocation($tree, $PRINTX);
        }
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(true, $list_expr.tree);
            setLocation($tree, $PRINTLNX);
        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree = $if_then_else.tree;
    

        }
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While($condition.tree, $body.tree);
            setLocation($tree, $WHILE);
        }
    | RETURN expr SEMI {
            assert($expr.tree != null);
            $tree = new Return($expr.tree);
            setLocation($tree, $RETURN);
            
        }
    ;

if_then_else returns[IfThenElse tree]
@init {
    java.util.List<AbstractExpr> elseIfConditions = new java.util.ArrayList<>();//gérant les elsifs
    java.util.List<ListInst> IfImbriquefI = new java.util.ArrayList<>();//gérant les elsifs
 

}
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
            // ListInst elseDeIf = new ListInst();//else courant vide
            // $tree = new IfThenElse($condition.tree,$li_if.tree, elseDeIf);
            // setLocation($tree, $if1);
            //on fait rien pour le moment
            
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
            //créer un nouveau IfThenElse imbriqué
            elseIfConditions.add($elsif_cond.tree);
            IfImbriquefI.add($elsif_li.tree);
           
      })*
      (ELSE OBRACE li_else=list_inst CBRACE {
            //reconstruction des if imbriqués
            if(IfImbriquefI.isEmpty()){
                    $tree = new IfThenElse($condition.tree, $li_if.tree, $li_else.tree);
                    //
                    setLocation($tree,$if1);
                    //
            }
            else{
                IfThenElse currElse = new IfThenElse(elseIfConditions.get(elseIfConditions.size()-1),
                                                        IfImbriquefI.get(IfImbriquefI.size()-1),$li_else.tree);
                //
                setLocation(currElse,$if1);
                //
                for (int i = elseIfConditions.size() - 2;i >=0; i--){ 
                    ListInst wrapperBody = new ListInst();
                    wrapperBody.add(currElse);
                    currElse = new IfThenElse(elseIfConditions.get(i),
                                                        IfImbriquefI.get(i),wrapperBody);
                    setLocation(currElse,$if1);
                }
                 //if initial
                 ListInst wrapperIfInit = new ListInst();
                 wrapperIfInit.add(currElse);
                $tree = new IfThenElse($condition.tree, $li_if.tree, wrapperIfInit);
                //
                setLocation($tree,$if1);
                // 
            }
            setLocation($tree, $ELSE);
        }
      )?
      {
        if ($tree == null){
            ListInst elseVide = new ListInst();
            if(IfImbriquefI.isEmpty()){
                    $tree = new IfThenElse($condition.tree, $li_if.tree, elseVide);
                    setLocation($tree,$if1);
            }
            else{
                IfThenElse currElse = new IfThenElse(elseIfConditions.get(elseIfConditions.size()-1),
                                                        IfImbriquefI.get(IfImbriquefI.size()-1), elseVide);
                setLocation(currElse,$if1);
                for (int i = elseIfConditions.size() - 2;i >=0; i--){ 
                    ListInst wrapperBody = new ListInst();
                    wrapperBody.add(currElse);
                    currElse = new IfThenElse(elseIfConditions.get(i),
                                                        IfImbriquefI.get(i),wrapperBody);
                    //
                    setLocation(currElse,$if1);
                    //
                }
                 //if initial
                 ListInst wrapperIfInit = new ListInst();
                 wrapperIfInit.add(currElse);
                $tree = new IfThenElse($condition.tree, $li_if.tree, wrapperIfInit);
                setLocation($tree,$if1);

            }
            setLocation($tree, $if1);
      }}
    ;


list_expr returns[ListExpr tree]
@init   {
        $tree = new ListExpr();

        }
    : (e1=expr {
            assert($e1.tree != null);
            $tree.add($e1.tree);
        }
       (COMMA e2=expr {
            assert($e2.tree != null);
            $tree.add($e2.tree);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : assign_expr {
            assert($assign_expr.tree != null);
            $tree = $assign_expr.tree;
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
            
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign((AbstractLValue)$e.tree, $e2.tree);
            setLocation($tree, $EQUALS);//faire monter des valeurs
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
    
        }
      )
    ;
    

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            $tree = $e.tree;
        }
    | e1=or_expr OR e2=and_expr {
            $tree = new Or($e1.tree,$e2.tree);
            setLocation($tree, $OR);
    }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;

        }
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($tree,$AND);
        }
    ;
eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree,$e2.tree);
            setLocation($tree, $EQEQ);  
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree,$e2.tree);
            setLocation($tree, $NEQ);
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree,$e2.tree);
            setLocation($tree, $LEQ);
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree,$e2.tree);
            setLocation($tree, $GEQ);
        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree,$e2.tree);
            setLocation($tree, $GT);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree,$e2.tree);
            setLocation($tree, $LT);
        }
    | e1=inequality_expr INSTANCEOF type {
            assert($e1.tree != null);
            assert($type.tree != null);
            $tree = new InstanceOf($e1.tree,$type.tree);
            setLocation($tree, $INSTANCEOF);
            
        }
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree,$e2.tree);
            setLocation($tree, $PLUS);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree,$e2.tree);
            setLocation($tree, $MINUS);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
        }
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree,$e2.tree);
            setLocation($tree, $TIMES);
        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree,$e2.tree);
            setLocation($tree, $SLASH);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree,$e2.tree);
            setLocation($tree, $PERCENT);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $op);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not($e.tree);
            setLocation($tree, $op);
        }
    | select_expr {
        
            assert($select_expr.tree != null);
            $tree = $select_expr.tree;
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
        assert($e.tree != null);
            $tree = $e.tree; 
        }
    | e1=select_expr op=DOT i=ident 
            
        (o=OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            $tree = new MethodCall($e1.tree, $i.tree, $args.tree);
            setLocation($tree, $op);   
        }
        | /* epsilon */ {

            $tree = new Selection($e1.tree, $i.tree);
            setLocation($tree,$op);
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
             }
    | m=ident OPARENT args=list_expr CPARENT {
            $tree = new MethodCall(null, $m.tree, $args.tree);
            setLocation($tree, $m.start);
        }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
        }
    | READINT OPARENT CPARENT {
            $tree = new ReadInt();
            setLocation($tree, $READINT);
        }
    | READFLOAT OPARENT CPARENT {
            $tree = new ReadFloat();
            setLocation($tree, $READFLOAT);
        }
    | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree, $NEW);
            
           
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($expr.tree, $type.tree);
            setLocation($tree, $cast);
           
        }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;
        }
    ;

type returns[AbstractIdentifier tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree, $ident.start);
        }
    ;

literal returns[AbstractExpr tree]
    : INT {
        try {
            $tree = new IntLiteral(Integer.parseInt($INT.text));
        } catch (NumberFormatException e) {
            // Utilise .line et .pos ici
            throw new RuntimeException($INT.line + ":" + $INT.pos + 
                ": L'entier '" + $INT.text + "' est trop grand pour un int 32 bits.");
        }
        setLocation($tree, $INT);
    }
    | fd=FLOAT {
        try {
            float val = Float.parseFloat($fd.text);
            if (Float.isInfinite(val)) { throw new NumberFormatException(); }
            $tree = new FloatLiteral(val);
        } catch (NumberFormatException e) {
            throw new RuntimeException($fd.line + ":" + $fd.pos + 
                ": Le flottant '" + $fd.text + "' est hors des limites.");
        }
        setLocation($tree, $fd);
    }
    | STRING {
        /* Enlever les quotations */
        String text = $STRING.text;
        text = text.substring(1, text.length() - 1);
        text = text.replace("\\\"", "\"").replace("\\\\", "\\");
        $tree = new StringLiteral(text);
        setLocation($tree, $STRING);
    }

    | TRUE {
        $tree = new BooleanLiteral(true);
        setLocation($tree, $TRUE);

        }
    | FALSE {
         $tree = new BooleanLiteral(false);
         setLocation($tree, $FALSE);
         
        }
    | THIS {
        $tree = new This(false);
        setLocation($tree, $THIS);
        }
    | NULL {
        $tree = new Null();
        setLocation($tree, $NULL);
        }
    ;

ident returns[AbstractIdentifier tree]
    : IDENT {
        assert($IDENT != null);
        Symbol sym = getDecacCompiler().createSymbol($IDENT.text);
        $tree = new Identifier(sym);
        setLocation($tree, $IDENT);
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init   {
            $tree = new ListDeclClass();
        }
    :
      (c1=class_decl {
        $tree.add($c1.tree);
        }
      )*
    ;

class_decl returns[AbstractDeclClass tree]
@init{
    ListDeclField fields = new ListDeclField();
    ListDeclMethod methods = new ListDeclMethod();
}
    : cn = CLASS name=ident superclass=class_extension OBRACE class_body[fields,methods] CBRACE {
        $tree = new DeclClass($name.tree, $superclass.tree, fields, methods);
        setLocation($tree, $cn);
        if ($superclass.tree.getLocation() == null) {
             setLocation($superclass.tree, $cn);
        }
    }
    ;

class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
        assert($ident.tree != null);
        $tree = $ident.tree;
        setLocation($ident.tree,$EXTENDS);
        }
    | /* epsilon */ {
        $tree = new Identifier(getDecacCompiler().createSymbol("Object"));
        }
    ;

class_body[ListDeclField fields,ListDeclMethod methods]
    : (m=decl_method {
        $methods.add($m.tree);
        }
      | decl_field_set[fields]
      )*
    ;

decl_field_set[ListDeclField fields]
    : v=visibility t=type list_decl_field[$v.tree,$t.tree,fields]
      SEMI
    ;

visibility returns [Visibility tree]
    : /* epsilon */ {
        $tree = Visibility.PUBLIC;
        }
    | PROTECTED {
        $tree = Visibility.PROTECTED;
        }
    ;

list_decl_field[Visibility v, AbstractIdentifier t, ListDeclField fields]
    : dv1=decl_field[v,t,fields]
        (COMMA dv2=decl_field[v,t,fields]
      )*
    ;

decl_field [Visibility v, AbstractIdentifier t, ListDeclField fields] 
@init{
    AbstractInitialization initialization =new NoInitialization();
}
    : i=ident (EQUALS e=expr { 
          initialization = new Initialization($e.tree);
          setLocation(initialization, $EQUALS); // <--- AJOUTE CECI !
      })?
    {
        if (initialization.getLocation() == null) {
             setLocation(initialization, $i.start);
        }
        DeclField field = new DeclField(v,t,$i.tree,initialization);
        setLocation(field, $i.start);
        $fields.add(field);
    }
    ;

decl_method returns [AbstractDeclMethod tree]
@init {
}
    : type id = ident OPARENT params=list_params CPARENT (block {

        AbstractMethodBody body = new MethodBody($block.decls, $block.insts);
        setLocation(body, $block.start);
        $tree = new DeclMethod($type.tree,$id.tree,$params.tree,body);
        setLocation($tree, $id.start);
        
        }
      | ASM OPARENT code=multi_line_string CPARENT SEMI {
   
        String asmName = $code.text;
        StringLiteral asmLit = new StringLiteral(asmName);
        AbstractMethodBody body = new MethodBodyAsm(asmLit);
        setLocation(body,$ASM);
        $tree = new DeclMethod($type.tree,$id.tree,$params.tree, body);
        setLocation($tree, $id.start);
        }
      ) 
    ;

list_params returns [ListDeclParam tree]
    : {$tree = new ListDeclParam();}
    (p1=param {
        $tree.add($p1.tree);
        } (COMMA p2=param {
            $tree.add($p2.tree);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns [AbstractDeclParam tree]
    : type ident {
        $tree = new DeclParam($type.tree,$ident.tree);
        setLocation($tree, $ident.start);
        }
    ;
